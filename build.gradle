plugins {
    id 'java'
    id 'nebula.release' version '4.2.0'
    id 'org.sonarqube' version '2.4'
    id 'ru.vyarus.quality' version '2.2.0'
    id 'jacoco'
    id "info.solidsoft.pitest" version "1.1.11"
    id 'org.springframework.boot' version '1.5.3.RELEASE'
}

group = 'uk.co.dajohnston.leaguetracker'

repositories {
    jcenter()
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestCompile.extendsFrom testRuntime
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integrationTest/java')
        }
        resources.srcDir file('src/integrationTest/resources')
    }
}

task integrationTest(type: Test) {
    group = LifecycleBasePlugin.VERIFICATION_GROUP
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

dependencies {
    compile 'org.springframework.boot:spring-boot-devtools'
    compile 'org.springframework.boot:spring-boot-starter-data-rest'
    compile 'org.springframework.boot:spring-boot-starter-data-jpa'
    compile 'org.springframework.security.oauth:spring-security-oauth2'
    compile 'com.h2database:h2'
    compile 'com.google.code.gson:gson:2.8.0'
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    integrationTestCompile 'org.springframework.boot:spring-boot-starter-test'
    integrationTestCompile 'info.cukes:cucumber-java:1.2.5'
    integrationTestCompile 'info.cukes:cucumber-junit:1.2.5'
    integrationTestCompile 'info.cukes:cucumber-spring:1.2.5'
    integrationTestCompile 'io.rest-assured:rest-assured:3.0.3'
}

quality {
    checkstyleVersion = '7.7'
    pmdVersion = '5.6.1'
}

jacoco {
    toolVersion = '0.7.9'
}

sonarqube {
    properties {
        property "sonar.exclusions", "**/Application.java"
        property "sonar.tests", "src/test/java, src/integrationTest/java"
        property "sonar.jacoco.reportPaths", "$buildDir/jacoco/test.exec, $buildDir/jacoco/integrationTest.exec"
    }
}
